// Contract: GovernanceManager (0x27ce6a70b572302cd5466591313a0029b38d7bb0)
// Multi-file source


// ===== FILE: contracts/GovernanceManager.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

import "contracts/utils/Address.sol";
import "contracts/utils/structs/EnumerableSet.sol";
import "contracts/utils/structs/EnumerableMap.sol";
import "contracts/proxy/utils/Initializable.sol";

import "libraries/DataTypes.sol";
import "libraries/ScaledMath.sol";
import "libraries/VaultsSnapshot.sol";
import "libraries/Errors.sol";

import "interfaces/IVault.sol";
import "interfaces/IGovernanceManager.sol";
import "interfaces/IVotingPowerAggregator.sol";
import "interfaces/ITierer.sol";
import "interfaces/ITierStrategy.sol";
import "interfaces/IBoundedERC20WithEMA.sol";

contract GovernanceManager is IGovernanceManager, Initializable {
    using Address for address;
    using ScaledMath for uint256;
    using EnumerableSet for EnumerableSet.UintSet;
    using EnumerableMap for EnumerableMap.AddressToUintMap;
    using VaultsSnapshot for DataTypes.VaultSnapshot[];

    uint256 internal constant _MULTISIG_SUNSET_PERIOD = 90 days;

    address public immutable multisig;
    IVotingPowerAggregator public immutable votingPowerAggregator;
    ITierer public immutable tierer;

    uint256 public multisigSunsetAt;
    IBoundedERC20WithEMA public bGYD;
    DataTypes.LimitUpgradeabilityParameters public limitUpgradeabilityParams;

    uint16 public proposalsCount;

    EnumerableSet.UintSet internal _activeProposals;
    EnumerableSet.UintSet internal _timelockedProposals;
    mapping(uint16 => DataTypes.Proposal) internal _proposals;
    mapping(uint16 => DataTypes.VaultSnapshot[]) internal _vaultSnapshots;

    mapping(address => mapping(uint16 => DataTypes.Ballot)) internal _votes;
    mapping(uint16 => mapping(DataTypes.Ballot => EnumerableMap.AddressToUintMap))
        internal _totals;

    modifier onlySelf() {
        if (msg.sender != address(this))
            revert Errors.NotAuthorized(msg.sender, address(this));
        _;
    }

    modifier onlyMultisig() {
        if (msg.sender != multisig)
            revert Errors.NotAuthorized(msg.sender, multisig);
        if (block.timestamp >= multisigSunsetAt) revert Errors.MultisigSunset();
        _;
    }

    constructor(
        address _multisig,
        IVotingPowerAggregator _votingPowerAggregator,
        ITierer _tierer
    ) {
        multisig = _multisig;
        votingPowerAggregator = _votingPowerAggregator;
        tierer = _tierer;
    }

    function initialize(
        IBoundedERC20WithEMA _bGYD,
        DataTypes.LimitUpgradeabilityParameters memory _params
    ) external initializer {
        bGYD = _bGYD;
        limitUpgradeabilityParams = _params;
        multisigSunsetAt = block.timestamp + _MULTISIG_SUNSET_PERIOD;
    }

    event MultisigSunsetAtUpdated(uint256 originalSunset, uint256 newSunset);

    function extendMultisigSunsetAt(
        uint256 extensionPeriod
    ) external onlyMultisig {
        uint256 oldMultisigSunsetAt = multisigSunsetAt;
        multisigSunsetAt += extensionPeriod;
        emit MultisigSunsetAtUpdated(oldMultisigSunsetAt, multisigSunsetAt);
    }

    event ProposalCreated(
        uint16 indexed id,
        address proposer,
        DataTypes.ProposalAction[] actions
    );

    function createProposal(
        DataTypes.ProposalAction[] calldata actions
    ) external override {
        require(actions.length > 0, "cannot create a proposal with no actions");

        DataTypes.Tier memory tier = _getTier(actions);

        // If a sufficiently large amount of GYD is bounded, this signifies that holders
        // are happy with the system and are against further high-level upgrades.
        // As a result, we should apply a higher tier if the proposed action has big impacts.
        if (_isUpgradeabilityLimited(tier.actionLevel)) {
            tier = _getLimitUpgradeabilityTier();
        }

        DataTypes.VaultVotingPower[] memory rawPower = votingPowerAggregator
            .getVotingPower(msg.sender, block.timestamp - 1);
        uint256 votingPowerPct = votingPowerAggregator
            .calculateWeightedPowerPct(rawPower);
        require(
            votingPowerPct > tier.proposalThreshold,
            "proposer doesn't have enough voting power to propose this action"
        );

        uint64 createdAt = uint64(block.timestamp);
        uint64 votingEndsAt = createdAt + tier.proposalLength;
        uint64 executableAt = votingEndsAt + tier.timeLockDuration;

        DataTypes.Proposal storage p = _proposals[proposalsCount];
        p.id = proposalsCount;
        p.proposer = msg.sender;
        p.createdAt = createdAt;
        p.actionLevel = tier.actionLevel;
        p.votingEndsAt = votingEndsAt;
        p.executableAt = executableAt;
        p.status = DataTypes.Status.Active;
        p.quorum = tier.quorum;
        p.voteThreshold = tier.voteThreshold;

        for (uint256 i = 0; i < actions.length; i++) {
            p.actions.push(actions[i]);
        }

        votingPowerAggregator.createVaultsSnapshot().persist(
            _vaultSnapshots[p.id]
        );

        proposalsCount = p.id + 1;
        _activeProposals.add(uint256(p.id));

        emit ProposalCreated(p.id, p.proposer, actions);
    }

    event VoteCast(
        uint16 indexed proposalId,
        address voter,
        DataTypes.Ballot vote
    );

    function vote(
        uint16 proposalId,
        DataTypes.Ballot ballot
    ) external override {
        DataTypes.Proposal storage proposal = _proposals[proposalId];
        require(proposal.createdAt != 0, "proposal does not exist");
        require(block.timestamp > proposal.createdAt, "voting has not started");

        require(
            proposal.votingEndsAt > uint64(block.timestamp),
            "voting is closed on this proposal"
        );

        require(
            ballot != DataTypes.Ballot.Undefined,
            "ballot must be cast For, Against, or Abstain"
        );

        DataTypes.VaultSnapshot[] memory vaultSnapshots = _vaultSnapshots[
            proposalId
        ];

        DataTypes.VaultVotingPower[] memory uvp = votingPowerAggregator
            .getVotingPower(
                msg.sender,
                proposal.createdAt,
                _vaultAddresses(vaultSnapshots)
            );

        DataTypes.Ballot existingVote = _votes[msg.sender][proposalId];

        bool isNewVote = existingVote == DataTypes.Ballot.Undefined;
        for (uint256 i = 0; i < uvp.length; i++) {
            DataTypes.VaultVotingPower memory vvp = uvp[i];

            // cancel out the previous vote if it was cast
            if (!isNewVote) {
                (, uint256 prevBallotTotal) = _totals[proposalId][existingVote]
                    .tryGet(vvp.vaultAddress);
                _totals[proposalId][existingVote].set(
                    vvp.vaultAddress,
                    prevBallotTotal - vvp.votingPower
                );
            }

            (, uint256 newBallotTotal) = _totals[proposalId][ballot].tryGet(
                vvp.vaultAddress
            );
            _totals[proposalId][ballot].set(
                vvp.vaultAddress,
                newBallotTotal + vvp.votingPower
            );
        }

        // Then update the record of this user's vote to the latest ballot and voting power
        _votes[msg.sender][proposalId] = ballot;

        emit VoteCast(proposalId, msg.sender, ballot);
    }

    function getVoteTotals(
        uint16 proposalId
    ) external view override returns (DataTypes.VoteTotals memory) {
        return _toVoteTotals(_totals[proposalId]);
    }

    function _toVoteTotals(
        mapping(DataTypes.Ballot => EnumerableMap.AddressToUintMap)
            storage totals
    ) internal view returns (DataTypes.VoteTotals memory) {
        EnumerableMap.AddressToUintMap storage forVotingPower = totals[
            DataTypes.Ballot.For
        ];
        DataTypes.VaultVotingPower[] memory forTotals = _toVotingPowers(
            forVotingPower
        );

        EnumerableMap.AddressToUintMap storage againstVotingPower = totals[
            DataTypes.Ballot.Against
        ];
        DataTypes.VaultVotingPower[] memory againstTotals = _toVotingPowers(
            againstVotingPower
        );

        EnumerableMap.AddressToUintMap storage abstentionsVotingPower = totals[
            DataTypes.Ballot.Abstain
        ];
        DataTypes.VaultVotingPower[] memory abstentionsTotals = _toVotingPowers(
            abstentionsVotingPower
        );

        return
            DataTypes.VoteTotals({
                _for: forTotals,
                against: againstTotals,
                abstentions: abstentionsTotals
            });
    }

    event ProposalTallied(
        uint16 indexed proposalId,
        DataTypes.Status status,
        DataTypes.ProposalOutcome outcome
    );

    function tallyVote(uint16 proposalId) external override {
        DataTypes.Proposal storage proposal = _proposals[proposalId];
        require(proposal.createdAt != 0, "proposal does not exist");

        require(
            proposal.status == DataTypes.Status.Active &&
                _activeProposals.contains(uint256(proposalId)),
            "proposal is not currently active"
        );

        require(
            uint64(block.timestamp) > proposal.votingEndsAt,
            "voting is ongoing for this proposal"
        );

        (
            uint256 forTotalPct,
            uint256 againstTotalPct,
            uint256 abstentionsTotalPct
        ) = _getCurrentPercentages(proposal);

        uint256 quorum = proposal.quorum;
        uint256 voteThreshold = proposal.voteThreshold;
        if (_isUpgradeabilityLimited(proposal.actionLevel)) {
            DataTypes.Tier memory tier = _getLimitUpgradeabilityTier();
            quorum = tier.quorum;
            voteThreshold = tier.voteThreshold;
        }

        uint256 combinedPct = forTotalPct +
            againstTotalPct +
            abstentionsTotalPct;
        if (combinedPct < quorum) {
            proposal.status = DataTypes.Status.Rejected;
            _activeProposals.remove(uint256(proposalId));
            emit ProposalTallied(
                proposalId,
                proposal.status,
                DataTypes.ProposalOutcome.QuorumNotMet
            );
            return;
        }

        uint256 result = 0;
        if (forTotalPct + againstTotalPct > 0) {
            result = forTotalPct.divDown(forTotalPct + againstTotalPct);
        }
        DataTypes.ProposalOutcome outcome = DataTypes.ProposalOutcome.Undefined;
        if (result >= voteThreshold) {
            proposal.status = DataTypes.Status.Queued;
            outcome = DataTypes.ProposalOutcome.Successful;
            _timelockedProposals.add(uint256(proposalId));
        } else {
            proposal.status = DataTypes.Status.Rejected;
            outcome = DataTypes.ProposalOutcome.ThresholdNotMet;
        }
        _activeProposals.remove(uint256(proposalId));
        emit ProposalTallied(proposalId, proposal.status, outcome);
    }

    function getCurrentPercentages(
        uint16 proposalId
    )
        external
        view
        override
        returns (uint256 for_, uint256 against, uint256 abstain)
    {
        DataTypes.Proposal storage proposal = _proposals[proposalId];
        require(proposal.createdAt != 0, "proposal does not exist");
        return _getCurrentPercentages(proposal);
    }

    function _getCurrentPercentages(
        DataTypes.Proposal storage proposal
    ) internal view returns (uint256 for_, uint256 against, uint256 abstain) {
        DataTypes.VaultSnapshot[] memory snapshot = _vaultSnapshots[
            proposal.id
        ];
        mapping(DataTypes.Ballot => EnumerableMap.AddressToUintMap)
            storage propTotals = _totals[proposal.id];
        for_ = snapshot.getBallotPercentage(propTotals[DataTypes.Ballot.For]);
        against = snapshot.getBallotPercentage(
            propTotals[DataTypes.Ballot.Against]
        );
        abstain = snapshot.getBallotPercentage(
            propTotals[DataTypes.Ballot.Abstain]
        );
    }

    function _toVotingPowers(
        EnumerableMap.AddressToUintMap storage map
    ) internal view returns (DataTypes.VaultVotingPower[] memory) {
        DataTypes.VaultVotingPower[]
            memory vvps = new DataTypes.VaultVotingPower[](map.length());
        for (uint256 i = 0; i < map.length(); i++) {
            (address key, uint256 value) = map.at(i);
            vvps[i] = DataTypes.VaultVotingPower({
                vaultAddress: key,
                votingPower: value
            });
        }

        return vvps;
    }

    event ProposalExecuted(uint16 indexed proposalId);

    function executeProposal(uint16 proposalId) external override {
        DataTypes.Proposal storage proposal = _proposals[proposalId];
        if (proposal.createdAt == uint64(0)) {
            revert("proposal does not exist");
        }

        require(
            proposal.status == DataTypes.Status.Queued &&
                _timelockedProposals.contains(uint256(proposalId)) &&
                uint64(block.timestamp) > proposal.executableAt,
            "proposal must be queued and ready to execute"
        );

        for (uint256 i = 0; i < proposal.actions.length; i++) {
            proposal.actions[i].target.functionCallWithValue(
                proposal.actions[i].data,
                proposal.actions[i].value,
                "proposal execution failed"
            );
        }
        proposal.status = DataTypes.Status.Executed;
        _timelockedProposals.remove(uint256(proposalId));
        emit ProposalExecuted(proposalId);
    }

    function createAndExecuteProposal(
        DataTypes.ProposalAction[] calldata actions
    ) external onlyMultisig {
        uint16 proposalId = proposalsCount++;
        DataTypes.Proposal storage p = _proposals[proposalId];
        p.id = proposalId;
        p.proposer = msg.sender;
        p.createdAt = uint64(block.timestamp);
        p.votingEndsAt = uint64(block.timestamp);
        p.executableAt = uint64(block.timestamp);
        p.status = DataTypes.Status.Executed;
        p.quorum = 0;
        p.voteThreshold = 0;

        for (uint256 i = 0; i < actions.length; i++) {
            DataTypes.ProposalAction memory action = actions[i];
            p.actions.push(action);
            action.target.functionCallWithValue(
                action.data,
                action.value,
                "proposal execution failed"
            );
        }
        emit ProposalCreated(proposalId, msg.sender, actions);
        emit ProposalExecuted(proposalId);
    }

    event ProposalVetoed(uint16 indexed proposalId);

    function vetoProposal(uint16 proposalId) external onlyMultisig {
        DataTypes.Proposal storage proposal = _proposals[proposalId];
        require(proposal.createdAt > 0, "proposal does not exist");

        require(
            proposal.status == DataTypes.Status.Active ||
                proposal.status == DataTypes.Status.Queued,
            "proposal must be active or queued"
        );

        proposal.status = DataTypes.Status.Vetoed;
        _activeProposals.remove(uint256(proposalId));
        _timelockedProposals.remove(uint256(proposalId));

        emit ProposalVetoed(proposalId);
    }

    event MultisigSunset();

    function sunsetMultisig() external onlySelf {
        multisigSunsetAt = block.timestamp;
        emit MultisigSunset();
    }

    function getBallot(
        address voter,
        uint16 proposalId
    ) external view returns (DataTypes.Ballot) {
        return _votes[voter][proposalId];
    }

    function getProposal(
        uint16 proposalId
    ) external view returns (DataTypes.Proposal memory) {
        return _proposals[proposalId];
    }

    function updateLimitUpgradeabilityParams(
        DataTypes.LimitUpgradeabilityParameters memory _params
    ) external onlySelf {
        limitUpgradeabilityParams = _params;
    }

    function listActiveProposals()
        external
        view
        returns (DataTypes.Proposal[] memory)
    {
        return _listProposals(_activeProposals.values());
    }

    function listTimelockedProposals()
        external
        view
        returns (DataTypes.Proposal[] memory)
    {
        return _listProposals(_timelockedProposals.values());
    }

    function _listProposals(
        uint256[] memory ids
    ) internal view returns (DataTypes.Proposal[] memory) {
        uint256 len = ids.length;
        DataTypes.Proposal[] memory proposals = new DataTypes.Proposal[](len);
        for (uint256 i = 0; i < len; i++) {
            proposals[i] = _proposals[uint16(ids[i])];
        }
        return proposals;
    }

    function _vaultAddresses(
        DataTypes.VaultSnapshot[] memory vaultSnapshots
    ) internal pure returns (address[] memory) {
        uint256 len = vaultSnapshots.length;
        address[] memory vaultAddresses = new address[](len);
        for (uint256 i = 0; i < len; i++) {
            vaultAddresses[i] = vaultSnapshots[i].vaultAddress;
        }
        return vaultAddresses;
    }

    function _getTier(
        DataTypes.ProposalAction[] memory actions
    ) internal view returns (DataTypes.Tier memory tier) {
        // Determine the tier associated with this proposal by taking the tier of the most impactful
        // action, determined by the tier's actionLevel parameter.
        DataTypes.ProposalAction memory action = actions[0];
        tier = tierer.getTier(action.target, action.data);
        for (uint256 i = 1; i < actions.length; i++) {
            DataTypes.Tier memory currentTier = tierer.getTier(
                actions[i].target,
                actions[i].data
            );
            if (currentTier.actionLevel > tier.actionLevel) {
                tier = currentTier;
            }
        }
    }

    function _isUpgradeabilityLimited(
        uint8 actionLevel
    ) internal view returns (bool) {
        return
            address(bGYD) != address(0) &&
            bGYD.totalSupply() >= limitUpgradeabilityParams.minBGYDSupply &&
            bGYD.boundedPctEMA() > limitUpgradeabilityParams.emaThreshold &&
            actionLevel >= limitUpgradeabilityParams.actionLevelThreshold;
    }

    function _getLimitUpgradeabilityTier()
        internal
        view
        returns (DataTypes.Tier memory)
    {
        // NOTE: tierStrategy is always static, so the calldata is unused
        return
            ITierStrategy(limitUpgradeabilityParams.tierStrategy).getTier("");
    }

    receive() external payable {}
}


// ===== FILE: contracts/utils/Address.sol =====
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling
     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.
     *
     * _Available since v4.8._
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        if (success) {
            if (returndata.length == 0) {
                // only check isContract if the call was successful and the return data is empty
                // otherwise we already know that it was a contract
                require(isContract(target), "Address: call to non-contract");
            }
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason or using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    function _revert(bytes memory returndata, string memory errorMessage) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert(errorMessage);
        }
    }
}


// ===== FILE: contracts/utils/structs/EnumerableSet.sol =====
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (utils/structs/EnumerableSet.sol)
// This file was procedurally generated from scripts/generate/templates/EnumerableSet.js.

pragma solidity ^0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 *
 * [WARNING]
 * ====
 * Trying to delete such a structure from storage will likely result in data corruption, rendering the structure
 * unusable.
 * See https://github.com/ethereum/solidity/pull/11843[ethereum/solidity#11843] for more info.
 *
 * In order to clean an EnumerableSet, you can either remove all elements one by one or create a fresh instance using an
 * array of EnumerableSet.
 * ====
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastValue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastValue;
                // Update the index for the moved value
                set._indexes[lastValue] = valueIndex; // Replace lastValue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        bytes32[] memory store = _values(set._inner);
        bytes32[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }
}


// ===== FILE: contracts/utils/structs/EnumerableMap.sol =====
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (utils/structs/EnumerableMap.sol)
// This file was procedurally generated from scripts/generate/templates/EnumerableMap.js.

pragma solidity ^0.8.0;

import "contracts/utils/structs/EnumerableSet.sol";

/**
 * @dev Library for managing an enumerable variant of Solidity's
 * https://solidity.readthedocs.io/en/latest/types.html#mapping-types[`mapping`]
 * type.
 *
 * Maps have the following properties:
 *
 * - Entries are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Entries are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableMap for EnumerableMap.UintToAddressMap;
 *
 *     // Declare a set state variable
 *     EnumerableMap.UintToAddressMap private myMap;
 * }
 * ```
 *
 * The following map types are supported:
 *
 * - `uint256 -> address` (`UintToAddressMap`) since v3.0.0
 * - `address -> uint256` (`AddressToUintMap`) since v4.6.0
 * - `bytes32 -> bytes32` (`Bytes32ToBytes32Map`) since v4.6.0
 * - `uint256 -> uint256` (`UintToUintMap`) since v4.7.0
 * - `bytes32 -> uint256` (`Bytes32ToUintMap`) since v4.7.0
 *
 * [WARNING]
 * ====
 * Trying to delete such a structure from storage will likely result in data corruption, rendering the structure
 * unusable.
 * See https://github.com/ethereum/solidity/pull/11843[ethereum/solidity#11843] for more info.
 *
 * In order to clean an EnumerableMap, you can either remove all elements one by one or create a fresh instance using an
 * array of EnumerableMap.
 * ====
 */
library EnumerableMap {
    using EnumerableSet for EnumerableSet.Bytes32Set;

    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Map type with
    // bytes32 keys and values.
    // The Map implementation uses private functions, and user-facing
    // implementations (such as Uint256ToAddressMap) are just wrappers around
    // the underlying Map.
    // This means that we can only create new EnumerableMaps for types that fit
    // in bytes32.

    struct Bytes32ToBytes32Map {
        // Storage of keys
        EnumerableSet.Bytes32Set _keys;
        mapping(bytes32 => bytes32) _values;
    }

    /**
     * @dev Adds a key-value pair to a map, or updates the value for an existing
     * key. O(1).
     *
     * Returns true if the key was added to the map, that is if it was not
     * already present.
     */
    function set(
        Bytes32ToBytes32Map storage map,
        bytes32 key,
        bytes32 value
    ) internal returns (bool) {
        map._values[key] = value;
        return map._keys.add(key);
    }

    /**
     * @dev Removes a key-value pair from a map. O(1).
     *
     * Returns true if the key was removed from the map, that is if it was present.
     */
    function remove(Bytes32ToBytes32Map storage map, bytes32 key) internal returns (bool) {
        delete map._values[key];
        return map._keys.remove(key);
    }

    /**
     * @dev Returns true if the key is in the map. O(1).
     */
    function contains(Bytes32ToBytes32Map storage map, bytes32 key) internal view returns (bool) {
        return map._keys.contains(key);
    }

    /**
     * @dev Returns the number of key-value pairs in the map. O(1).
     */
    function length(Bytes32ToBytes32Map storage map) internal view returns (uint256) {
        return map._keys.length();
    }

    /**
     * @dev Returns the key-value pair stored at position `index` in the map. O(1).
     *
     * Note that there are no guarantees on the ordering of entries inside the
     * array, and it may change when more entries are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32ToBytes32Map storage map, uint256 index) internal view returns (bytes32, bytes32) {
        bytes32 key = map._keys.at(index);
        return (key, map._values[key]);
    }

    /**
     * @dev Tries to returns the value associated with `key`. O(1).
     * Does not revert if `key` is not in the map.
     */
    function tryGet(Bytes32ToBytes32Map storage map, bytes32 key) internal view returns (bool, bytes32) {
        bytes32 value = map._values[key];
        if (value == bytes32(0)) {
            return (contains(map, key), bytes32(0));
        } else {
            return (true, value);
        }
    }

    /**
     * @dev Returns the value associated with `key`. O(1).
     *
     * Requirements:
     *
     * - `key` must be in the map.
     */
    function get(Bytes32ToBytes32Map storage map, bytes32 key) internal view returns (bytes32) {
        bytes32 value = map._values[key];
        require(value != 0 || contains(map, key), "EnumerableMap: nonexistent key");
        return value;
    }

    /**
     * @dev Same as {get}, with a custom error message when `key` is not in the map.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryGet}.
     */
    function get(
        Bytes32ToBytes32Map storage map,
        bytes32 key,
        string memory errorMessage
    ) internal view returns (bytes32) {
        bytes32 value = map._values[key];
        require(value != 0 || contains(map, key), errorMessage);
        return value;
    }

    // UintToUintMap

    struct UintToUintMap {
        Bytes32ToBytes32Map _inner;
    }

    /**
     * @dev Adds a key-value pair to a map, or updates the value for an existing
     * key. O(1).
     *
     * Returns true if the key was added to the map, that is if it was not
     * already present.
     */
    function set(
        UintToUintMap storage map,
        uint256 key,
        uint256 value
    ) internal returns (bool) {
        return set(map._inner, bytes32(key), bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the key was removed from the map, that is if it was present.
     */
    function remove(UintToUintMap storage map, uint256 key) internal returns (bool) {
        return remove(map._inner, bytes32(key));
    }

    /**
     * @dev Returns true if the key is in the map. O(1).
     */
    function contains(UintToUintMap storage map, uint256 key) internal view returns (bool) {
        return contains(map._inner, bytes32(key));
    }

    /**
     * @dev Returns the number of elements in the map. O(1).
     */
    function length(UintToUintMap storage map) internal view returns (uint256) {
        return length(map._inner);
    }

    /**
     * @dev Returns the element stored at position `index` in the set. O(1).
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintToUintMap storage map, uint256 index) internal view returns (uint256, uint256) {
        (bytes32 key, bytes32 value) = at(map._inner, index);
        return (uint256(key), uint256(value));
    }

    /**
     * @dev Tries to returns the value associated with `key`. O(1).
     * Does not revert if `key` is not in the map.
     */
    function tryGet(UintToUintMap storage map, uint256 key) internal view returns (bool, uint256) {
        (bool success, bytes32 value) = tryGet(map._inner, bytes32(key));
        return (success, uint256(value));
    }

    /**
     * @dev Returns the value associated with `key`. O(1).
     *
     * Requirements:
     *
     * - `key` must be in the map.
     */
    function get(UintToUintMap storage map, uint256 key) internal view returns (uint256) {
        return uint256(get(map._inner, bytes32(key)));
    }

    /**
     * @dev Same as {get}, with a custom error message when `key` is not in the map.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryGet}.
     */
    function get(
        UintToUintMap storage map,
        uint256 key,
        string memory errorMessage
    ) internal view returns (uint256) {
        return uint256(get(map._inner, bytes32(key), errorMessage));
    }

    // UintToAddressMap

    struct UintToAddressMap {
        Bytes32ToBytes32Map _inner;
    }

    /**
     * @dev Adds a key-value pair to a map, or updates the value for an existing
     * key. O(1).
     *
     * Returns true if the key was added to the map, that is if it was not
     * already present.
     */
    function set(
        UintToAddressMap storage map,
        uint256 key,
        address value
    ) internal returns (bool) {
        return set(map._inner, bytes32(key), bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the key was removed from the map, that is if it was present.
     */
    function remove(UintToAddressMap storage map, uint256 key) internal returns (bool) {
        return remove(map._inner, bytes32(key));
    }

    /**
     * @dev Returns true if the key is in the map. O(1).
     */
    function contains(UintToAddressMap storage map, uint256 key) internal view returns (bool) {
        return contains(map._inner, bytes32(key));
    }

    /**
     * @dev Returns the number of elements in the map. O(1).
     */
    function length(UintToAddressMap storage map) internal view returns (uint256) {
        return length(map._inner);
    }

    /**
     * @dev Returns the element stored at position `index` in the set. O(1).
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintToAddressMap storage map, uint256 index) internal view returns (uint256, address) {
        (bytes32 key, bytes32 value) = at(map._inner, index);
        return (uint256(key), address(uint160(uint256(value))));
    }

    /**
     * @dev Tries to returns the value associated with `key`. O(1).
     * Does not revert if `key` is not in the map.
     */
    function tryGet(UintToAddressMap storage map, uint256 key) internal view returns (bool, address) {
        (bool success, bytes32 value) = tryGet(map._inner, bytes32(key));
        return (success, address(uint160(uint256(value))));
    }

    /**
     * @dev Returns the value associated with `key`. O(1).
     *
     * Requirements:
     *
     * - `key` must be in the map.
     */
    function get(UintToAddressMap storage map, uint256 key) internal view returns (address) {
        return address(uint160(uint256(get(map._inner, bytes32(key)))));
    }

    /**
     * @dev Same as {get}, with a custom error message when `key` is not in the map.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryGet}.
     */
    function get(
        UintToAddressMap storage map,
        uint256 key,
        string memory errorMessage
    ) internal view returns (address) {
        return address(uint160(uint256(get(map._inner, bytes32(key), errorMessage))));
    }

    // AddressToUintMap

    struct AddressToUintMap {
        Bytes32ToBytes32Map _inner;
    }

    /**
     * @dev Adds a key-value pair to a map, or updates the value for an existing
     * key. O(1).
     *
     * Returns true if the key was added to the map, that is if it was not
     * already present.
     */
    function set(
        AddressToUintMap storage map,
        address key,
        uint256 value
    ) internal returns (bool) {
        return set(map._inner, bytes32(uint256(uint160(key))), bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the key was removed from the map, that is if it was present.
     */
    function remove(AddressToUintMap storage map, address key) internal returns (bool) {
        return remove(map._inner, bytes32(uint256(uint160(key))));
    }

    /**
     * @dev Returns true if the key is in the map. O(1).
     */
    function contains(AddressToUintMap storage map, address key) internal view returns (bool) {
        return contains(map._inner, bytes32(uint256(uint160(key))));
    }

    /**
     * @dev Returns the number of elements in the map. O(1).
     */
    function length(AddressToUintMap storage map) internal view returns (uint256) {
        return length(map._inner);
    }

    /**
     * @dev Returns the element stored at position `index` in the set. O(1).
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressToUintMap storage map, uint256 index) internal view returns (address, uint256) {
        (bytes32 key, bytes32 value) = at(map._inner, index);
        return (address(uint160(uint256(key))), uint256(value));
    }

    /**
     * @dev Tries to returns the value associated with `key`. O(1).
     * Does not revert if `key` is not in the map.
     */
    function tryGet(AddressToUintMap storage map, address key) internal view returns (bool, uint256) {
        (bool success, bytes32 value) = tryGet(map._inner, bytes32(uint256(uint160(key))));
        return (success, uint256(value));
    }

    /**
     * @dev Returns the value associated with `key`. O(1).
     *
     * Requirements:
     *
     * - `key` must be in the map.
     */
    function get(AddressToUintMap storage map, address key) internal view returns (uint256) {
        return uint256(get(map._inner, bytes32(uint256(uint160(key)))));
    }

    /**
     * @dev Same as {get}, with a custom error message when `key` is not in the map.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryGet}.
     */
    function get(
        AddressToUintMap storage map,
        address key,
        string memory errorMessage
    ) internal view returns (uint256) {
        return uint256(get(map._inner, bytes32(uint256(uint160(key))), errorMessage));
    }

    // Bytes32ToUintMap

    struct Bytes32ToUintMap {
        Bytes32ToBytes32Map _inner;
    }

    /**
     * @dev Adds a key-value pair to a map, or updates the value for an existing
     * key. O(1).
     *
     * Returns true if the key was added to the map, that is if it was not
     * already present.
     */
    function set(
        Bytes32ToUintMap storage map,
        bytes32 key,
        uint256 value
    ) internal returns (bool) {
        return set(map._inner, key, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the key was removed from the map, that is if it was present.
     */
    function remove(Bytes32ToUintMap storage map, bytes32 key) internal returns (bool) {
        return remove(map._inner, key);
    }

    /**
     * @dev Returns true if the key is in the map. O(1).
     */
    function contains(Bytes32ToUintMap storage map, bytes32 key) internal view returns (bool) {
        return contains(map._inner, key);
    }

    /**
     * @dev Returns the number of elements in the map. O(1).
     */
    function length(Bytes32ToUintMap storage map) internal view returns (uint256) {
        return length(map._inner);
    }

    /**
     * @dev Returns the element stored at position `index` in the set. O(1).
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32ToUintMap storage map, uint256 index) internal view returns (bytes32, uint256) {
        (bytes32 key, bytes32 value) = at(map._inner, index);
        return (key, uint256(value));
    }

    /**
     * @dev Tries to returns the value associated with `key`. O(1).
     * Does not revert if `key` is not in the map.
     */
    function tryGet(Bytes32ToUintMap storage map, bytes32 key) internal view returns (bool, uint256) {
        (bool success, bytes32 value) = tryGet(map._inner, key);
        return (success, uint256(value));
    }

    /**
     * @dev Returns the value associated with `key`. O(1).
     *
     * Requirements:
     *
     * - `key` must be in the map.
     */
    function get(Bytes32ToUintMap storage map, bytes32 key) internal view returns (uint256) {
        return uint256(get(map._inner, key));
    }

    /**
     * @dev Same as {get}, with a custom error message when `key` is not in the map.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryGet}.
     */
    function get(
        Bytes32ToUintMap storage map,
        bytes32 key,
        string memory errorMessage
    ) internal view returns (uint256) {
        return uint256(get(map._inner, key, errorMessage));
    }
}


// ===== FILE: contracts/proxy/utils/Initializable.sol =====
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (proxy/utils/Initializable.sol)

pragma solidity ^0.8.2;

import "contracts/utils/Address.sol";

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since proxied contracts do not make use of a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * The initialization functions use a version number. Once a version number is used, it is consumed and cannot be
 * reused. This mechanism prevents re-execution of each "step" but allows the creation of new initialization steps in
 * case an upgrade adds a module that needs to be initialized.
 *
 * For example:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * contract MyToken is ERC20Upgradeable {
 *     function initialize() initializer public {
 *         __ERC20_init("MyToken", "MTK");
 *     }
 * }
 * contract MyTokenV2 is MyToken, ERC20PermitUpgradeable {
 *     function initializeV2() reinitializer(2) public {
 *         __ERC20Permit_init("MyToken");
 *     }
 * }
 * ```
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 *
 * [CAUTION]
 * ====
 * Avoid leaving a contract uninitialized.
 *
 * An uninitialized contract can be taken over by an attacker. This applies to both a proxy and its implementation
 * contract, which may impact the proxy. To prevent the implementation contract from being used, you should invoke
 * the {_disableInitializers} function in the constructor to automatically lock it when it is deployed:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * /// @custom:oz-upgrades-unsafe-allow constructor
 * constructor() {
 *     _disableInitializers();
 * }
 * ```
 * ====
 */
abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     * @custom:oz-retyped-from bool
     */
    uint8 private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Triggered when the contract has been initialized or reinitialized.
     */
    event Initialized(uint8 version);

    /**
     * @dev A modifier that defines a protected initializer function that can be invoked at most once. In its scope,
     * `onlyInitializing` functions can be used to initialize parent contracts.
     *
     * Similar to `reinitializer(1)`, except that functions marked with `initializer` can be nested in the context of a
     * constructor.
     *
     * Emits an {Initialized} event.
     */
    modifier initializer() {
        bool isTopLevelCall = !_initializing;
        require(
            (isTopLevelCall && _initialized < 1) || (!Address.isContract(address(this)) && _initialized == 1),
            "Initializable: contract is already initialized"
        );
        _initialized = 1;
        if (isTopLevelCall) {
            _initializing = true;
        }
        _;
        if (isTopLevelCall) {
            _initializing = false;
            emit Initialized(1);
        }
    }

    /**
     * @dev A modifier that defines a protected reinitializer function that can be invoked at most once, and only if the
     * contract hasn't been initialized to a greater version before. In its scope, `onlyInitializing` functions can be
     * used to initialize parent contracts.
     *
     * A reinitializer may be used after the original initialization step. This is essential to configure modules that
     * are added through upgrades and that require initialization.
     *
     * When `version` is 1, this modifier is similar to `initializer`, except that functions marked with `reinitializer`
     * cannot be nested. If one is invoked in the context of another, execution will revert.
     *
     * Note that versions can jump in increments greater than 1; this implies that if multiple reinitializers coexist in
     * a contract, executing them in the right order is up to the developer or operator.
     *
     * WARNING: setting the version to 255 will prevent any future reinitialization.
     *
     * Emits an {Initialized} event.
     */
    modifier reinitializer(uint8 version) {
        require(!_initializing && _initialized < version, "Initializable: contract is already initialized");
        _initialized = version;
        _initializing = true;
        _;
        _initializing = false;
        emit Initialized(version);
    }

    /**
     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the
     * {initializer} and {reinitializer} modifiers, directly or indirectly.
     */
    modifier onlyInitializing() {
        require(_initializing, "Initializable: contract is not initializing");
        _;
    }

    /**
     * @dev Locks the contract, preventing any future reinitialization. This cannot be part of an initializer call.
     * Calling this in the constructor of a contract will prevent that contract from being initialized or reinitialized
     * to any version. It is recommended to use this to lock implementation contracts that are designed to be called
     * through proxies.
     *
     * Emits an {Initialized} event the first time it is successfully executed.
     */
    function _disableInitializers() internal virtual {
        require(!_initializing, "Initializable: contract is initializing");
        if (_initialized < type(uint8).max) {
            _initialized = type(uint8).max;
            emit Initialized(type(uint8).max);
        }
    }

    /**
     * @dev Internal function that returns the initialized version. Returns `_initialized`
     */
    function _getInitializedVersion() internal view returns (uint8) {
        return _initialized;
    }

    /**
     * @dev Internal function that returns the initialized version. Returns `_initializing`
     */
    function _isInitializing() internal view returns (bool) {
        return _initializing;
    }
}


// ===== FILE: libraries/DataTypes.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

import "contracts/utils/structs/EnumerableMap.sol";

library DataTypes {
    enum Status {
        Undefined,
        Active,
        Rejected,
        Queued,
        Executed,
        Vetoed
    }

    struct ProposalAction {
        address target;
        bytes data;
        uint256 value;
    }

    struct Proposal {
        uint64 createdAt;
        uint64 executableAt;
        uint64 votingEndsAt;
        uint64 voteThreshold;
        uint64 quorum;
        uint16 id;
        uint8 actionLevel;
        address proposer;
        Status status;
        ProposalAction[] actions;
    }

    struct PendingWithdrawal {
        uint256 id;
        uint256 withdrawableAt;
        uint256 amount;
        address to;
        address delegate;
    }

    struct VaultWeightSchedule {
        VaultWeightConfiguration[] vaults;
        uint256 startsAt;
        uint256 endsAt;
    }

    struct VaultWeightConfiguration {
        address vaultAddress;
        uint256 initialWeight;
        uint256 targetWeight;
    }

    struct VaultWeight {
        address vaultAddress;
        uint256 currentWeight;
        uint256 initialWeight;
        uint256 targetWeight;
    }

    struct VaultVotingPower {
        address vaultAddress;
        uint256 votingPower;
    }

    struct Tier {
        uint64 quorum;
        uint64 proposalThreshold;
        uint64 voteThreshold;
        uint32 timeLockDuration;
        uint32 proposalLength;
        uint8 actionLevel;
    }

    struct EmergencyRecoveryProposal {
        uint64 createdAt;
        uint64 completesAt;
        Status status;
        bytes payload;
        EnumerableMap.AddressToUintMap vetos;
    }

    enum Ballot {
        Undefined,
        For,
        Against,
        Abstain
    }

    struct VoteTotals {
        VaultVotingPower[] _for;
        VaultVotingPower[] against;
        VaultVotingPower[] abstentions;
    }

    struct VaultSnapshot {
        address vaultAddress;
        uint256 weight;
        uint256 totalVotingPower;
    }

    enum ProposalOutcome {
        Undefined,
        QuorumNotMet,
        ThresholdNotMet,
        Successful
    }

    struct LimitUpgradeabilityParameters {
        uint8 actionLevelThreshold;
        uint256 emaThreshold;
        uint256 minBGYDSupply;
        address tierStrategy;
    }

    struct Delegation {
        address delegate;
        uint256 amount;
    }
}


// ===== FILE: libraries/ScaledMath.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

library ScaledMath {
    uint256 internal constant ONE = 1e18;

    function mulDown(uint256 a, uint256 b) internal pure returns (uint256) {
        return (a * b) / ONE;
    }

    function divDown(uint256 a, uint256 b) internal pure returns (uint256) {
        return (a * ONE) / b;
    }

    function changeScale(
        uint256 a,
        uint256 from,
        uint256 to
    ) internal pure returns (uint256) {
        if (from == to) return a;
        else if (from < to) return a * 10 ** (to - from);
        else return a / 10 ** (from - to);
    }
}


// ===== FILE: libraries/VaultsSnapshot.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

import "contracts/utils/structs/EnumerableMap.sol";

import "libraries/DataTypes.sol";
import "libraries/ScaledMath.sol";

library VaultsSnapshot {
    using EnumerableMap for EnumerableMap.AddressToUintMap;
    using ScaledMath for uint256;

    function getBallotPercentage(
        DataTypes.VaultSnapshot[] memory snapshots,
        EnumerableMap.AddressToUintMap storage vaultPowers
    ) internal view returns (uint256 votingPowerPct) {
        for (uint256 i; i < snapshots.length; i++) {
            DataTypes.VaultSnapshot memory snapshot = snapshots[i];
            (, uint256 ballotPower) = vaultPowers.tryGet(snapshot.vaultAddress);
            votingPowerPct += ballotPower
                .divDown(snapshot.totalVotingPower)
                .mulDown(snapshot.weight);
        }
    }

    /// @dev this simply appends, so the storage must be clean
    function persist(
        DataTypes.VaultSnapshot[] memory snapshots,
        DataTypes.VaultSnapshot[] storage cleanStorage
    ) internal {
        require(cleanStorage.length == 0, "storage must be clean");
        for (uint256 i; i < snapshots.length; i++) {
            cleanStorage.push(snapshots[i]);
        }
    }
}


// ===== FILE: libraries/Errors.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

library Errors {
    error DuplicatedVault(address vault);
    error InvalidTotalWeight(uint256 totalWeight);
    error NotAuthorized(address actual, address expected);
    error InvalidVotingPowerUpdate(
        uint256 actualTotalPower,
        uint256 givenTotalPower
    );
    error MultisigSunset();

    error ZeroDivision();
}


// ===== FILE: interfaces/IVault.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

import "libraries/VotingPowerHistory.sol";

interface IVault {
    function getRawVotingPower(address account) external view returns (uint256);

    function getCurrentRecord(
        address account
    ) external view returns (VotingPowerHistory.Record memory);

    function getRawVotingPower(
        address account,
        uint256 timestamp
    ) external view returns (uint256);

    function getTotalRawVotingPower() external view returns (uint256);

    function getVaultType() external view returns (string memory);
}


// ===== FILE: libraries/VotingPowerHistory.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

import "libraries/ScaledMath.sol";

library VotingPowerHistory {
    using VotingPowerHistory for History;
    using VotingPowerHistory for Record;
    using ScaledMath for uint256;

    struct Record {
        uint256 at;
        uint256 baseVotingPower;
        uint256 multiplier;
        int256 netDelegatedVotes;
    }

    function zeroRecord() internal pure returns (Record memory) {
        return
            Record({
                at: 0,
                baseVotingPower: 0,
                multiplier: ScaledMath.ONE,
                netDelegatedVotes: 0
            });
    }

    function total(Record memory record) internal pure returns (uint256) {
        return
            uint256(
                int256(record.baseVotingPower.mulDown(record.multiplier)) +
                    record.netDelegatedVotes
            );
    }

    struct History {
        mapping(address => Record[]) votes;
        mapping(address => mapping(address => uint256)) _delegations;
        mapping(address => uint256) _delegatedToOthers;
        mapping(address => uint256) _delegatedToSelf;
    }

    event VotesDelegated(address from, address to, uint256 amount);
    event VotesUndelegated(address from, address to, uint256 amount);

    function updateVotingPower(
        History storage history,
        address for_,
        uint256 baseVotingPower,
        uint256 multiplier,
        int256 netDelegatedVotes
    ) internal returns (Record memory) {
        Record[] storage votesFor = history.votes[for_];
        Record memory updatedRecord = Record({
            at: block.timestamp,
            baseVotingPower: baseVotingPower,
            multiplier: multiplier,
            netDelegatedVotes: netDelegatedVotes
        });
        Record memory lastRecord = history.currentRecord(for_);
        if (lastRecord.at == block.timestamp && votesFor.length > 0) {
            votesFor[votesFor.length - 1] = updatedRecord;
        } else {
            history.votes[for_].push(updatedRecord);
        }
        return updatedRecord;
    }

    function getVotingPower(
        History storage history,
        address for_,
        uint256 at
    ) internal view returns (uint256) {
        (, Record memory record) = binarySearch(history.votes[for_], at);
        return record.total();
    }

    function currentRecord(
        History storage history,
        address for_
    ) internal view returns (Record memory) {
        Record[] memory records = history.votes[for_];
        if (records.length == 0) {
            return zeroRecord();
        } else {
            return records[records.length - 1];
        }
    }

    function binarySearch(
        Record[] memory records,
        uint256 at
    ) internal view returns (bool found, Record memory) {
        return _binarySearch(records, at, 0, records.length);
    }

    function _binarySearch(
        Record[] memory records,
        uint256 at,
        uint256 startIdx,
        uint256 endIdx
    ) internal view returns (bool found, Record memory) {
        if (startIdx >= endIdx) {
            return (false, zeroRecord());
        }

        if (endIdx - startIdx == 1) {
            Record memory rec = records[startIdx];
            return rec.at <= at ? (true, rec) : (false, zeroRecord());
        }

        uint256 midIdx = (endIdx + startIdx) / 2;
        Record memory lowerBound = records[midIdx - 1];
        Record memory upperBound = records[midIdx];
        if (lowerBound.at <= at && at < upperBound.at) {
            return (true, lowerBound);
        } else if (upperBound.at <= at) {
            return _binarySearch(records, at, midIdx, endIdx);
        } else {
            return _binarySearch(records, at, startIdx, midIdx);
        }
    }

    function delegateVote(
        History storage history,
        address from,
        address to,
        uint256 amount
    ) internal {
        Record memory fromCurrent = history.currentRecord(from);

        uint256 availableToDelegate = fromCurrent.baseVotingPower.mulDown(
            fromCurrent.multiplier
        ) - history._delegatedToOthers[from];
        require(
            availableToDelegate >= amount,
            "insufficient balance to delegate"
        );

        history._delegatedToSelf[to] += amount;
        history._delegatedToOthers[from] += amount;
        history._delegations[from][to] += amount;

        history.updateVotingPower(
            from,
            fromCurrent.baseVotingPower,
            fromCurrent.multiplier,
            history.netDelegatedVotingPower(from)
        );
        Record memory toCurrent = history.currentRecord(to);
        history.updateVotingPower(
            to,
            toCurrent.baseVotingPower,
            toCurrent.multiplier,
            history.netDelegatedVotingPower(to)
        );

        emit VotesDelegated(from, to, amount);
    }

    function undelegateVote(
        History storage history,
        address from,
        address to,
        uint256 amount
    ) internal {
        require(
            history._delegations[from][to] >= amount,
            "user has not delegated enough to delegate"
        );

        history._delegatedToSelf[to] -= amount;
        history._delegatedToOthers[from] -= amount;
        history._delegations[from][to] -= amount;

        Record memory fromCurrent = history.currentRecord(from);
        history.updateVotingPower(
            from,
            fromCurrent.baseVotingPower,
            fromCurrent.multiplier,
            history.netDelegatedVotingPower(from)
        );
        Record memory toCurrent = history.currentRecord(to);
        history.updateVotingPower(
            to,
            toCurrent.baseVotingPower,
            toCurrent.multiplier,
            history.netDelegatedVotingPower(to)
        );

        emit VotesUndelegated(from, to, amount);
    }

    function netDelegatedVotingPower(
        History storage history,
        address who
    ) internal view returns (int256) {
        return
            int256(history._delegatedToSelf[who]) -
            int256(history._delegatedToOthers[who]);
    }

    function delegatedVotingPower(
        History storage history,
        address who
    ) internal view returns (uint256) {
        return history._delegatedToOthers[who];
    }

    function updateMultiplier(
        History storage history,
        address who,
        uint256 multiplier
    ) internal {
        Record memory current = history.currentRecord(who);
        require(current.multiplier <= multiplier, "cannot decrease multiplier");
        history.updateVotingPower(
            who,
            current.baseVotingPower,
            multiplier,
            current.netDelegatedVotes
        );
    }
}


// ===== FILE: interfaces/IGovernanceManager.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

import "libraries/DataTypes.sol";
import "interfaces/IVotingPowerAggregator.sol";

interface IGovernanceManager {
    function createProposal(
        DataTypes.ProposalAction[] calldata actions
    ) external;

    function vote(uint16 proposalId, DataTypes.Ballot ballot) external;

    function getVoteTotals(
        uint16 proposalId
    ) external view returns (DataTypes.VoteTotals memory);

    function tallyVote(uint16 proposalId) external;

    function getCurrentPercentages(
        uint16 proposalId
    ) external view returns (uint256 for_, uint256 against, uint256 abstain);

    function executeProposal(uint16 proposalId) external;

    function getBallot(
        address voter,
        uint16 proposalId
    ) external view returns (DataTypes.Ballot);

    function getProposal(
        uint16 proposalId
    ) external view returns (DataTypes.Proposal memory);

    function listActiveProposals()
        external
        view
        returns (DataTypes.Proposal[] memory);

    function listTimelockedProposals()
        external
        view
        returns (DataTypes.Proposal[] memory);

    function votingPowerAggregator()
        external
        view
        returns (IVotingPowerAggregator);

    function multisig() external view returns (address);

    function createAndExecuteProposal(
        DataTypes.ProposalAction[] calldata actions
    ) external;

    function vetoProposal(uint16 proposalId) external;

    function extendMultisigSunsetAt(uint256 extensionPeriod) external;
}


// ===== FILE: interfaces/IVotingPowerAggregator.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

import "libraries/DataTypes.sol";

interface IVotingPowerAggregator {
    function createVaultsSnapshot()
        external
        view
        returns (DataTypes.VaultSnapshot[] memory snapshots);

    function getVotingPower(
        address account,
        uint256 timestamp
    ) external view returns (DataTypes.VaultVotingPower[] memory);

    function getVotingPower(
        address account,
        uint256 timestamp,
        address[] memory vaults
    ) external view returns (DataTypes.VaultVotingPower[] memory);

    function calculateWeightedPowerPct(
        DataTypes.VaultVotingPower[] calldata vaultVotingPowers
    ) external view returns (uint256);

    function listVaults()
        external
        view
        returns (DataTypes.VaultWeight[] memory);

    function getVaultWeight(address vault) external view returns (uint256);

    function setSchedule(
        DataTypes.VaultWeightSchedule calldata schedule
    ) external;
}


// ===== FILE: interfaces/ITierer.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

import "libraries/DataTypes.sol";

interface ITierer {
    function getTier(
        address _contract,
        bytes calldata payload
    ) external view returns (DataTypes.Tier memory);
}


// ===== FILE: interfaces/ITierStrategy.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

import "libraries/DataTypes.sol";

interface ITierStrategy {
    function getTier(
        bytes calldata payload
    ) external view returns (DataTypes.Tier memory);
}


// ===== FILE: interfaces/IBoundedERC20WithEMA.sol =====
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.17;

import "node_modules/@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol";

interface IBoundedERC20WithEMA is IERC20Upgradeable {
    function boundedPctEMA() external view returns (uint256);
}


// ===== FILE: node_modules/@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol =====
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20Upgradeable {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
}

